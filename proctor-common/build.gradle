plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

indeedOss.activateFeature 'library'
indeedLibrary.name = 'proctor-common'

dependencies {
    implementation project(':proctor-tomcat-deps')
    implementation ('com.indeed:util-core:1.0.49-1cfb888') {
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    implementation ('com.indeed:util-varexport:1.0.49-1cfb888') {
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    implementation libs.springCore
    implementation libs.guava
    implementation libs.commonsLang
    implementation libs.jsr305
    implementation libs.jacksonDatabind
    implementation libs.log4jApi
    implementation libs.log4jCore
    testImplementation('org.slf4j:slf4j-api:1.7.32')
    testImplementation libs.junit
    testImplementation libs.assertj
    testImplementation libs.easymock
    testImplementation libs.easymockclassext
    testImplementation libs.annotationApi
    testImplementation libs.mockito
    testImplementation libs.jsr305
    compileOnly libs.lombok
    annotationProcessor libs.lombok

    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok
}

// Need to remove all dependenceis that get added or will cause issues upstream when projects include
// Plan to remove in PROC-1290: Remove Proctor Shading of Javax El libraries
shadowJar {
    archiveClassifier.set('')
    dependencies {
        exclude(dependency('org.springframework:spring-core'))
        exclude(dependency('com.google.guava:guava'))
        exclude(dependency('org.apache.commons:commons-lang3'))
        exclude(dependency('org.apache.commons-logging:commons-logging'))
        exclude(dependency('org.apache.logging.log4j:log4j'))
        exclude(dependency('org.apache.logging:logging-parent'))
        exclude(dependency('com.google.code.findbugs:jsr305'))
        exclude(dependency('com.fasterxml.jackson.core:jackson-databind'))
        exclude(dependency('org.apache.logging.log4j:log4j-api'))
        exclude(dependency('org.apache.logging.log4j:log4j-core'))
        exclude(dependency('com.indeed:util-varexport'))
        exclude(dependency('com.indeed:util-core'))
        exclude(dependency('com.fasterxml.jackson.core:jackson-core'))
        exclude(dependency('com.fasterxml.jackson.core:jackson-annotations'))
        exclude(project(':proctor-tomcat-deps'))
    }
    exclude('META-INF/web-fragment.xml')
    exclude('META-INF/services/javax.el.ExpressionFactory')
    relocate 'javax.el', 'com.indeed.shaded.javax.el7'
    relocate 'org.apache.el', 'com.indeed.shaded.org.apache.el7'
    relocate 'org.apache.tomcat', 'com.indeed.shaded.org.apache.el7'
}

shadowJar.mustRunAfter jar